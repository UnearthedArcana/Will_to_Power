
//____________________________________________________________________________________
//____________________________________________________________________________________
//
//									PSIONIC FIGHTER
//____________________________________________________________________________________
//____________________________________________________________________________________


DEFINE_ACTION_FUNCTION soulblade_kit BEGIN 


//ADD SOULBLADE_______________________________________________________________________
//
ADD_KIT ~D5_SOULBLADE~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                       T   S           F   Q             S S
//10               L S                     W   C           L   U             W I
//9                A M             B L S   O   I   W       A   A             O N
//8                R A             A O H   H   M   A       I   R C   S       R G
//7                G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6                E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5                S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4                W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3                O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2                R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1                D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5_SOULBLADE      0 0 0 0 0 0 0 0~
~D5_SOULBLADE      0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS___________________________________________________________________
//                   STR DEX CON INT WIS CHR
~D5_SOULBLADE          0   0   9   12  9   0~

//KIT STAT MODIFIERS__________________________________________________________________
//                    STR DEX CON INT WIS CHR
~D5_SOULBLADE          0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT____________________________________________________
//                    STR DEX CON INT WIS CHR
~D5_SOULBLADE          0   0   0   17  0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT__________________________________________________
//                    STR DEX CON INT WIS CHR
~D5_SOULBLADE          0   0   0   15  0   0~

//ALIGNMENT RESTRICTIONS______________________________________________________________
//                    LG LN LE NG TN NE CG CN CE
~D5_SOULBLADE          1  1  1  1  1  1  0  0  0~

//DUAL CLASS OPTIONS__________________________________________________________________
//                 	  FT CL MA TH DR RA
~D5_SOULBLADE          0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~will_to_power/data/kit/d5psifi.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~~

//UNUSABLE FLAGS AND KIT BASE CLASS___________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00008000 8~

//HIGH LEVEL ABILITIES ABBREVIATION___________________________________________________
~Fi0~

//TOB STARTING EQUIPMENT______________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION__________________________________________________
SAY @301
SAY @302
SAY @303
//____________________________________________________________________________________

//EE KIT EXTRAS_______________________________________________________________________
// 
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@303)
		STR_VAR
			kit_name = D5_SOULBLADE
			clswpbon = ~1 0 3~
			clsrcreq = ~1 1 1 1 1 1 1~
	END
END
//____________________________________________________________________________________


ACTION_IF !(FILE_EXISTS_IN_GAME ~d5ps000.bam~) BEGIN
  COPY ~will_to_power/data/d5ps000.bam~ ~override~
END
COPY ~will_to_power/data/kit/d5pzifc.spl~ ~override~
  SAY NAME1 ~initialize psionic abilities~
  SAY UNIDENTIFIED_DESC ~initialize psionic abilities~
  LPF ALTER_SPELL_HEADER INT_VAR target = 5 location = 4 STR_VAR icon = ~d5ps000~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = ~d5pzifc~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 9 STR_VAR resource = ~d5pswt_~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = ~d5pswt0~ END
COPY ~will_to_power/data/kit/d5pzifc.eff~ ~override~
COPY ~will_to_power/data/kit/d5pzifc.cre~ ~override~
COMPILE ~will_to_power/data/kit/d5pzifc.baf~
//____________________________________________________________________________________


LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 0
	u_plate_armor 		= 0
	u_club_staff	 	= 2
	u_hammers	 		= 2
	u_maces 	 		= 2
	u_flails 			= 2
	u_axes 				= 2
	u_daggers 			= 2
	u_short_swords		= 2
	u_long_swords		= 2
	u_scimitars 		= 2
	u_katanas 			= 2
	u_bastard_swords 	= 2
	u_2hand_swords 		= 2
	u_halberds 			= 2
	u_spears 	 		= 2
	u_darts 			= 2
	u_slings	 		= 2
	u_bows 				= 2
	u_crossbows 		= 2
  STR_VAR
    clab_name 		= ~d5psifi~
	class 			= ~fighter_cleric~	 
	sphere_list		= ~helm_spheres~
END

// no need to actually apply spheres - if the sphere system already exists, we'll just side-step it

ADD_KIT ~D5_SOULBLADE_MENU~

//WEAPON PROFICIENCIES________________________________________________________________
//11           		                            T   S           F   Q             S S
//10           		    L S                     W   C           L   U             W I
//9            		    A M             B L S   O   I   W       A   A             O N
//8            		    R A             A O H   H   M   A       I   R C   S       R G
//7            		    G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            		    E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            		    S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            		    W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            		    O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            		    R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            		    D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5_SOULBLADE_MENU      0 0 0 0 0 0 0 0~
~D5_SOULBLADE_MENU      0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS___________________________________________________________________
//                    STR DEX CON INT WIS CHR
~D5_SOULBLADE_MENU     0   0   9   12  9   0~

//KIT STAT MODIFIERS__________________________________________________________________
//                    STR DEX CON INT WIS CHR
~D5_SOULBLADE_MENU     0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT____________________________________________________
//                    STR DEX CON INT WIS CHR
~D5_SOULBLADE_MENU     0   0   0   17  0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT__________________________________________________
//                    STR DEX CON INT WIS CHR
~D5_SOULBLADE_MENU     18  18  18  18  18  18~

//ALIGNMENT RESTRICTIONS______________________________________________________________
//                    LG LN LE NG TN NE CG CN CE
~D5_SOULBLADE_MENU     1  1  1  1  1  1  0  0  0~

//DUAL CLASS OPTIONS__________________________________________________________________
//                    FT CL MA TH DR RA
~D5_SOULBLADE_MENU     0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~will_to_power/data/kit/d5psifm.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_F_D K_F_E K_F_G K_F_H K_F_HE K_F_HL K_F_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS___________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00008000 2~

//HIGH LEVEL ABILITIES ABBREVIATION___________________________________________________
~Fi0~

//TOB STARTING EQUIPMENT______________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION__________________________________________________
SAY @301
SAY @302
SAY @303
//____________________________________________________________________________________

//EE KIT EXTRAS_______________________________________________________________________
// 
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@303)
		STR_VAR
			kit_name = D5_SOULBLADE_MENU
			hpclass = ~HPROG~
			clswpbon = ~1 0 3~
			clsrcreq = ~1 1 1 1 1 1 1~
	END
END
//____________________________________________________________________________________

LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 0
	u_plate_armor 		= 0
	u_club_staff	 	= 2
	u_hammers	 		= 2
	u_maces 	 		= 2
	u_flails 			= 2
	u_axes 				= 2
	u_daggers 			= 2
	u_short_swords		= 2
	u_long_swords		= 2
	u_scimitars 		= 2
	u_katanas 			= 2
	u_bastard_swords 	= 2
	u_2hand_swords 		= 2
	u_halberds 			= 2
	u_spears 	 		= 2
	u_darts 			= 2
	u_slings	 		= 2
	u_bows 				= 2
	u_crossbows 		= 2
  STR_VAR
    clab_name 		= ~d5psifm~
	class 			= ~fighter_cleric~
END
//____________________________________________________________________________________


LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
	LAF apply_fnp_usability STR_VAR clab_name = ~d5psifi~ END
	LAF apply_fnp_usability STR_VAR clab_name = ~d5psifm~ END
END


//LAF no_cleric_spells STR_VAR clab_table = ~d5psirn~ END


// append psi.2da, and apply qdmulti
COPY_EXISTING ~d5psifi.2da~ ~override~
  LPM remove_blank_lines
  APPEND_FILE ~override/d5_psi.2da~
IF_EXISTS


LAF qd_multiclass
  STR_VAR
	kit_name = ~D5_SOULBLADE~
	kit_clab = ~d5psifi~
	base_class = ~P~
END


//KIT IDENTIFIER FOR PATCHING LATER___________________________________________________
//
OUTER_SET soulblade_code = (0x4000 + %D5_SOULBLADE%)
//____________________________________________________________________________________


//patch for Project Mind______________________________________________________________
//
APPEND ~splprot.2da~ ~D5_KIT_IS%TAB%152%TAB%-1%TAB%1~ UNLESS ~D5_KIT_IS~
COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_KIT_IS~ BEGIN
	    SET kit_is_row = %row%
	  END
	END
BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~projimag.spl~ BEGIN
  COPY_EXISTING ~projimag.spl~ ~override~
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %soulblade_code% parameter2 = %kit_is_row% timing = 1 STR_VAR resource = ~d5ps511b~ END
  BUT_ONLY
END
//____________________________________________________________________________________


//SCRIPTS ETC.________________________________________________________________________
//
EXTEND_TOP ~d5_psi.bcs~ ~will_to_power/data/kit/d5_soulb.baf~
//____________________________________________________________________________________


//NO WILD TALENT______________________________________________________________________
//
COPY_EXISTING ~d5pswt_.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR insert-point = 0 opcode = 318 target = 1 parameter1 = %soulblade_code% parameter2 = %kit_is_row% timing = 0 duration = 0 STR_VAR resource = ~d5pswt_~ END
IF_EXISTS BUT_ONLY
//____________________________________________________________________________________


//NO CLERIC STRONGHOLD________________________________________________________________
//
COPY_EXISTING ~ar0900.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(!Kit(Player1,GODHELM)[ %TAB%%LNL%%MNL%%WNL%]+!Kit(Player1,GODTALOS)\)~ ~\1 !Kit(Player1,D5_SOULBLADE)~
    REPLACE_TEXTUALLY ~\(!Kit(Player1,GODLATHANDER)[ %TAB%%LNL%%MNL%%WNL%]+!Kit(Player1,GODTALOS)\)~ ~\1 !Kit(Player1,D5_SOULBLADE)~
    REPLACE_TEXTUALLY ~\(!Kit(Player1,GODLATHANDER)[ %TAB%%LNL%%MNL%%WNL%]+!Kit(Player1,GODHELM)\)~ ~\1 !Kit(Player1,D5_SOULBLADE)~
  END
IF_EXISTS BUT_ONLY
//____________________________________________________________________________________


//PSIONIC HLA TABLES__________________________________________________________________
//
LAF action_remove_hla STR_VAR kit_name = ~D5_SOULBLADE~ remove_ability = ~GA_SPCL902~ END	
LAF action_remove_hla STR_VAR kit_name = ~D5_SOULBLADE~ remove_ability = ~GA_SPCL903~ END
LAF action_remove_hla STR_VAR kit_name = ~D5_SOULBLADE~ remove_ability = ~GA_SPCL908~ END
LAF action_add_hla STR_VAR kit_name = ~D5_SOULBLADE~ ability = ~AP_D5PSIFZ~ num_allowed = ~6~ END
LAF action_add_hla STR_VAR kit_name = ~D5_SOULBLADE~ ability = ~AP_D5_RFD1~ num_allowed = ~3~ END
LAF action_add_hla STR_VAR kit_name = ~D5_SOULBLADE~ ability = ~GA_D5_RFD5~ num_allowed = ~1~ END
ACTION_IF FILE_EXISTS_IN_GAME ~tg#toug.spl~ BEGIN
  LAF action_add_hla STR_VAR kit_name = ~D5_SOULBLADE~ ability = ~AP_TG#TOUG~ num_allowed = ~5~ END
END
//____________________________________________________________________________________


//make items work with psionics_______________________________________________________
//
// potion of clarity: +12 psps
COPY_EXISTING ~potn21.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %soulblade_code% parameter2 = %kit_is_row% timing = 1 STR_VAR resource = ~d5ptn21~ END
IF_EXISTS BUT_ONLY

//potion of power: +12 psps...?
COPY_EXISTING ~potn41.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %soulblade_code% parameter2 = %kit_is_row% timing = 1 STR_VAR resource = ~d5ptn21~ END
IF_EXISTS BUT_ONLY

// illithid brine: +30 psps (and resist psionics)
COPY_EXISTING ~misc9y.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %soulblade_code% parameter2 = %kit_is_row% timing = 1 STR_VAR resource = ~d5msc9y~ END
IF_EXISTS BUT_ONLY
//____________________________________________________________________________________


//WEAR ELVEN CHAIN___________________________________________________________________
//
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_echan.d5~ BEGIN
	INCLUDE ~will_to_power/lib/elven_chain.tpa~
END
//___________________________________________________________________________________

END	//	end function

