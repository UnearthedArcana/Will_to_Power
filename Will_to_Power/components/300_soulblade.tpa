
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								PSIONICS
//__________________________________________________________________________________
//__________________________________________________________________________________



//ADD SOULBLADE_______________________________________________________________________
//
ADD_KIT ~D5_SOULBLADE~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                    T   S           F   Q             S S
//10            L S                     W   C           L   U             W I
//9             A M             B L S   O   I   W       A   A             O N
//8             R A             A O H   H   M   A       I   R C   S       R G
//7             G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6             E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5             S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4             W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3             O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2             R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1             D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5_SOULBLADE   0 0 0 0 0 0 0 0~
~D5_SOULBLADE   0 0 0 0 0 0 0 0 0 3 3 3 0 3 3 3 3 3 3 0 3 3 3 1 1 1 1 1 2 0 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_SOULBLADE      0   0   9   12  9   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_SOULBLADE      0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_SOULBLADE      17  0   0   0   17  0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_SOULBLADE      15  0   0   0   15  0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5_SOULBLADE      1  1  1  1  1  1  0  0  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5_SOULBLADE      0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~will_to_power/data/d5psiwar.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_F_H K_F_G K_F_E K_F_HE K_F_HL K_F_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00010000 2~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Fi0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG20 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @301
SAY @302
SAY @303
//___________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@303)
		STR_VAR
			kit_name = D5_SOULBLADE
			clswpbon = ~1 0 2~
			hpclass = ~HPPRS~
	END
END
//____________________________________________________________________________________

//DON'T USE SCROLLS/WANDS____________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_rf84n.spl~ BEGIN
	APPEND ~d5psiwar.2da~ ~ABILITY    AP_D5_RF84N ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****  ~
END
ACTION_IF FILE_EXISTS_IN_GAME ~d5_nscrl.spl~ BEGIN
	APPEND ~d5psirog.2da~ ~ABILITY    AP_D5_NSCRL ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****  ~
END
//___________________________________________________________________________________

//KIT IDENTIFIER FOR PATCHING LATER_________________________________________________
//
COPY_EXISTING ~kit.ids~ ~override~
	COUNT_2DA_COLS k_cols
	READ_2DA_ENTRIES_NOW k_rows k_cols
	FOR (k_row = 1; k_row < k_rows; ++k_row) BEGIN
	  READ_2DA_ENTRY_FORMER k_rows k_row 1 ~k_kit~
	  PATCH_IF ~%k_kit%~ STRING_EQUAL_CASE ~D5_SOULBLADE~ BEGIN
	    SET psion_row = %k_row%
	    READ_2DA_ENTRY_FORMER k_rows psion_row 0 psoul_code
	  END
	END
BUT_ONLY
//___________________________________________________________________________________

//patch for Project Mind_____________________________________________________________
//
APPEND ~splprot.2da~ ~D5_KIT_EQUAL%TAB%152%TAB%-1%TAB%1~ UNLESS ~D5_KIT_EQUAL~

COPY_EXISTING ~splprot.2da~ ~override~
  COUNT_2DA_COLS cols
  READ_2DA_ENTRIES_NOW rows cols
  FOR (row = 1; row < rows; ++row) BEGIN
	READ_2DA_ENTRY_FORMER rows row 0 ~stat~
	PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_KIT_EQUAL~ BEGIN
	  SET stat_kit = %row%
	END
  END
BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~projimag.spl~ BEGIN
  COPY_EXISTING ~projimag.spl~ ~override~
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter2 = %stat_kit% parameter1 = %psoul_code% timing = 1 STR_VAR resource = ~d5ps907b~ END
  BUT_ONLY
END
//__________________________________________________________________________________

//SCRIPTS ETC.______________________________________________________________________
//
EXTEND_TOP ~d5_psi.bcs~ ~will_to_power/data/d5_soulb.baf~
COMPILE ~will_to_power/data/d5_soulb.d~ 
//__________________________________________________________________________________


/*
//NU FEAT FILES_____________________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_rfeat.d5~ BEGIN
  COPY_EXISTING ~d5_feat.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
	  REPLACE_TEXTUALLY ~SetGlobal("D5_PSI","GLOBAL",0)~ ~SetGlobal("D5_PSI","GLOBAL",1)~
	END
  BUT_ONLY
  EXTEND_TOP ~d5_feat.bcs~ ~might_and_guile/psi/d5_fpsi.baf~
  COMPILE ~might_and_guile/psi/d5_soulb.d~ 
  COMPILE ~might_and_guile/psi/d5_psion.d~ 
END
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_rfeat.d5~ BEGIN
  ACTION_FOR_EACH feat IN ~d5feata~ ~d5featb~ ~d5featc~ ~d5featd~ ~d5feate~ ~d5featf~ ~d5featg~ ~d5feath~ ~d5feati~ ~d5featj~ ~d5featk~ ~d5featl~ ~d5featm~ ~d5featn~ ~d5feato~ ~d5featp~ ~d5featq~ ~d5featr~ ~d5feats~ ~d5featt~ BEGIN
	COPY ~might_and_guile/feats/%feat%.spl~ ~override~
		SAY NAME1 @20099
		SAY UNIDENTIFIED_DESC @20100
		WRITE_ASCII 0x3a ~d5_feat~ #8
		LPF ALTER_SPELL_HEADER STR_VAR icon = ~d5_feat~ END
  END
  COPY ~might_and_guile/feats/d5_feat.bam~ ~override~
  COPY ~might_and_guile/feats/d5_feat.eff~ ~override~ 
  COPY ~might_and_guile/feats/d5_feat.cre~ ~override~ 
  COMPILE ~might_and_guile/psi/d5_feat.baf~ 
  COMPILE ~might_and_guile/psi/d5_soulb.d~ 
  COMPILE ~might_and_guile/psi/d5_psion.d~ 
END
//__________________________________________________________________________________
*/


//PSIONIC HLA TABLES________________________________________________________________
//
LAF action_remove_hla STR_VAR kit_name = ~D5_SOULBLADE~ remove_ability = ~GA_SPCL902~ END	
LAF action_remove_hla STR_VAR kit_name = ~D5_SOULBLADE~ remove_ability = ~GA_SPCL903~ END
LAF action_remove_hla STR_VAR kit_name = ~D5_SOULBLADE~ remove_ability = ~GA_SPCL908~ END
LAF action_add_hla STR_VAR kit_name = ~D5_SOULBLADE~ ability = ~AP_D5PSIFZ~ num_allowed = ~6~ END
LAF action_add_hla STR_VAR kit_name = ~D5_SOULBLADE~ ability = ~AP_D5_RFD1~ num_allowed = ~3~ END
LAF action_add_hla STR_VAR kit_name = ~D5_SOULBLADE~ ability = ~GA_D5_RFD5~ num_allowed = ~1~ END
ACTION_IF FILE_EXISTS_IN_GAME ~tg#toug.spl~ BEGIN
  LAF action_add_hla STR_VAR kit_name = ~D5_SOULBLADE~ ability = ~AP_TG#TOUG~ num_allowed = ~5~ END
END
//__________________________________________________________________________________

