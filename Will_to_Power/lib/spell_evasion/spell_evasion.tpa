
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//							IWD-style Spell Evasion
//__________________________________________________________________________________
//__________________________________________________________________________________

/*
SAVES:
1 = spells
2 = breath
4 = death
8 = rod/wand
16 = petrify
*/

// TO DO:
// - apply evasion to items
// - set amul25 (amul of spell warding) to provide evasion...? oO

//patch spells for evasion__________________________________________________________
//
DEFINE_ACTION_FUNCTION add_evade_spell INT_VAR evasion_spellstate = 252 evasion_save = 2 STR_VAR evade_spell = ~this_spell~ evade_prefix = ~prefix~ BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~evade%evasion_spellstate%.2da~ BEGIN
	COPY_EXISTING ~evade%evasion_spellstate%.2da~ ~override~
	  COUNT_2DA_ROWS 2 rows
	  READ_2DA_ENTRY (rows - 1) 0 2 last_entry
	BUT_ONLY
	OUTER_SET evasion_ind = %last_entry%
  END
  ACTION_TO_UPPER %evade_prefix%
  ACTION_IF NOT FILE_EXISTS_IN_GAME ~evade%evasion_spellstate%.2da~ BEGIN
	COPY ~%MOD_FOLDER%/lib/spell_evasion/evasion.2da~ ~override/evade%evasion_spellstate%.2da~
	OUTER_SET evasion_ind = 0
  END
  COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~STATE~ BEGIN
	    SET state_row = %row%
	  END
	END
  BUT_ONLY
  ACTION_IF FILE_EXISTS_IN_GAME ~%evade_spell%.spl~ BEGIN
	ACTION_IF NOT FILE_CONTAINS_EVALUATED (~evade%evasion_spellstate%.2da~ ~%evade_spell%~) BEGIN
	OUTER_SET evasion_ind = (evasion_ind + 1)
	  COPY_EXISTING ~%evade_spell%.spl~ ~override~
		LPF DELETE_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 63 END
		LPF DELETE_EFFECT INT_VAR match_opcode = 72 match_parameter1 = 4 match_duration = 0 END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 177 target = 2 parameter1 = 0 parameter2 = 2 timing = 0 duration = 1 STR_VAR resource = EVAL ~%evade_prefix%%evasion_ind%~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 2 parameter2 = 0 timing = 0 duration = 1 savingthrow = %evasion_save% STR_VAR resource = EVAL ~%evade_prefix%%evasion_ind%~ END
// 	if traditional evasion, do not evade when helpless
		PATCH_IF %evasion_spellstate% = 252 BEGIN	
		  LPF DELETE_EFFECT INT_VAR match_opcode = 318 match_parameter2 = 136 END
		  LPF CLONE_EFFECT INT_VAR match_opcode = 318 match_duration = 1 match_savingthrow = %evasion_save% parameter1 = 2 parameter2 = %state_row% savingthrow = 0 STR_VAR insert = ~first~ END 		// berserk
		  LPF CLONE_EFFECT INT_VAR match_opcode = 318 match_duration = 1 match_savingthrow = %evasion_save% parameter1 = 262144 parameter2 = %state_row% savingthrow = 0 STR_VAR insert = ~first~ END	// blind
		  LPF CLONE_EFFECT INT_VAR match_opcode = 318 match_duration = 1 match_savingthrow = %evasion_save% parameter1 = 8192 parameter2 = %state_row% savingthrow = 0 STR_VAR insert = ~first~ END 	// charmed
		  LPF CLONE_EFFECT INT_VAR match_opcode = 318 match_duration = 1 match_savingthrow = %evasion_save% parameter1 = 0x80000000 parameter2 = %state_row% savingthrow = 0 STR_VAR insert = ~first~ END // confused
		  LPF CLONE_EFFECT INT_VAR match_opcode = 318 match_duration = 1 match_savingthrow = %evasion_save% parameter1 = 1048576 parameter2 = %state_row% savingthrow = 0 STR_VAR insert = ~first~ END	// feebleminded
		  LPF CLONE_EFFECT INT_VAR match_opcode = 318 match_duration = 1 match_savingthrow = %evasion_save% parameter1 = 32 parameter2 = %state_row% savingthrow = 0 STR_VAR insert = ~first~ END 		// helpless
		  LPF CLONE_EFFECT INT_VAR match_opcode = 318 match_duration = 1 match_savingthrow = %evasion_save% parameter1 = 4 parameter2 = %state_row% savingthrow = 0 STR_VAR insert = ~first~ END 		// panicked
		  LPF CLONE_EFFECT INT_VAR match_opcode = 318 match_duration = 1 match_savingthrow = %evasion_save% parameter1 = 1 parameter2 = %state_row% savingthrow = 0 STR_VAR insert = ~first~ END 		// sleeping
		  LPF CLONE_EFFECT INT_VAR match_opcode = 318 match_duration = 1 match_savingthrow = %evasion_save% parameter1 = 65536 parameter2 = %state_row% savingthrow = 0 STR_VAR insert = ~first~ END 	// slowed
		  LPF CLONE_EFFECT INT_VAR match_opcode = 318 match_duration = 1 match_savingthrow = %evasion_save% parameter1 = 8 parameter2 = %state_row% savingthrow = 0 STR_VAR insert = ~first~ END 		// stunned
		END
	  CREATE EFF ~%evade_prefix%%evasion_ind%~
		WRITE_LONG 0x10 324
		WRITE_LONG 0x14 2
		WRITE_LONG 0x1c %evasion_spellstate%
		WRITE_LONG 0x20 110
		WRITE_LONG 0x24 0
		WRITE_LONG 0x28 1
		WRITE_SHORT 0x2c 100
		WRITE_EVALUATED_ASCII 0x30 ~%evade_spell%~ #8
		WRITE_LONG 0x90 1
		WRITE_EVALUATED_ASCII 0x94 ~%evade_prefix%%evasion_ind%~ #8
	  APPEND ~evade%evasion_spellstate%.2da~ ~%evasion_ind%%TAB%%evade_spell%%TAB%%evade_prefix%_%evasion_ind%~
	END
  END
END
//__________________________________________________________________________________


//list of spells to patch___________________________________________________________
//
ACTION_DEFINE_ASSOCIATIVE_ARRAY evade_spells BEGIN		
	spcl722 		=> 1
	spdr301 		=> 1
	spin132 		=> 1
	spin169 		=> 1
	sppr302 		=> 1
	sppr304 		=> 1
	sppr313 		=> 1
	sppr314 		=> 1
	sppr323d 		=> 1
	sppr324 		=> 1
	sppr325 		=> 1
	sppr419 		=> 1
	sppr420b 		=> 1
	sppr426 		=> 1
	sppr427 		=> 1
	sppr503 		=> 1
	sppr519 		=> 1
	sppr603d 		=> 1
	sppr616 		=> 1
	sppr617 		=> 1
	sppr618 		=> 1
	sppr619 		=> 1
	sppr705 		=> 1
	sppr738 		=> 1
	spwi103 		=> 1
	spwi105 		=> 1
	spwi204 		=> 1
	spwi304 		=> 1
	spwi308 		=> 1
	spwi327 		=> 1
	spwi328 		=> 1
	spwi404 		=> 1
	spwi431 		=> 1
	spwi503 		=> 1
	spwi523 		=> 1
	spwi709 		=> 1
	spwi714 		=> 1
	spwi724 		=> 1
	spwi806 		=> 1
	spwi812 		=> 1
	spwish32 		=> 1
END
ACTION_IF (optional_evade = 1) BEGIN
  ACTION_DEFINE_ASSOCIATIVE_ARRAY evade_spells BEGIN		
	abzaway 		=> 1
	balshld2 		=> 1
	bdbarbde 		=> 1
	bdbelblz 		=> 1
	bdbelinf 		=> 1
	bdbow05 		=> 1
	bdcaela4 		=> 1
	bddagg05 		=> 1
	bdmepwat 		=> 1
	bdpflame 		=> 1
	bdsha12a 		=> 1
	bdspl01 		=> 1
	bdunslgu 		=> 1
	bdvenoms 		=> 1
	bdwyrmb 		=> 1
	ohbdrag1 		=> 1
	ohbeflam 		=> 1
	ohbicew 		=> 1
	ohbwi304 		=> 1
	ohbwi308 		=> 1
	ohdmask 		=> 1
	spblun29 		=> 1
	spcl237d 		=> 1
	spcl911b 		=> 1
	spctmd01 		=> 1
	spdd03  		=> 1
	spdr201 		=> 1
	spdr601 		=> 1
	spimix01 		=> 1
	spin134 		=> 1
	spin160 		=> 1
	spin175 		=> 1
	spin191 		=> 1
	firau3d6 		=> 1
	spin935 		=> 1
	spogre01 		=> 1
	sppr105 		=> 1
	sppr698d 		=> 1
	sppr720 		=> 1
	sppr725d 		=> 1
	sppr901 		=> 1
	sppr984 		=> 1
	sppr985 		=> 1
	sppr987 		=> 1
	spwi001 		=> 1
	spwi002 		=> 1
	spwi003 		=> 1
	spwi005 		=> 1
	spwi006 		=> 1
	spwi007 		=> 1
	spwi008 		=> 1
	spwi009 		=> 1
	spwi010 		=> 1
	spwi011 		=> 1
	spwi012 		=> 1
	spwi013 		=> 1
	spwi015 		=> 1
	spwi017 		=> 1
	spwi021 		=> 1
	spwi024 		=> 1
	spwi025 		=> 1
	spwi026 		=> 1
	spwi027 		=> 1
	spwi033 		=> 1
	spwi034 		=> 1
	spwi101 		=> 1
	spwi112 		=> 1
	spwi211 		=> 1
	spwi215 		=> 1
	spwi299 		=> 1
	spwi303 		=> 1
	spwi308 		=> 1
	spwi313 		=> 1
	spwi327 		=> 1
	spwi399 		=> 1
	sppr951d 		=> 1
	sppr952d 		=> 1
	spwi430 		=> 1
	spwi524 		=> 1
	spwi615 		=> 1
	spwi628 		=> 1
	spwi712 		=> 1
	spwi818 		=> 1
	spwi911 		=> 1
	spwi918 		=> 1
	spwi922 		=> 1
	spwi925 		=> 1
	cdstaf12		=> 1
// add all beholder abilities???
  END
  ACTION_IF MOD_IS_INSTALLED ~item_rev~ ~0~ BEGIN
	ACTION_DEFINE_ASSOCIATIVE_ARRAY evade_spells BEGIN
	  potn26			=> 1
	  potn27			=> 1
	  sppr606d			=> 1
	  wand03			=> 1
	  wand04			=> 1
	  wand05a			=> 1
	  wand05b			=> 1
	  wand06			=> 1
	  wand07			=> 1
	  wand11			=> 1
	END
  END
  ACTION_DEFINE_ASSOCIATIVE_ARRAY evade_items BEGIN		
	amul01 			=> 1
	arow06 			=> 1
	frag01 			=> 1
	fragsap 		=> 1
	frosty 			=> 1
	mdk2mtov 		=> 1
	melfmet 		=> 1
	ohbshld1 		=> 1
	prox01 			=> 1
	ring20 			=> 1
	ttwand 			=> 1
  END
  ACTION_IF NOT MOD_IS_INSTALLED ~item_rev~ ~0~ BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY evade_items BEGIN		
	  fireseed			=> 1
	  potn13 			=> 1
	  potn26 			=> 1
	  potn27 			=> 1
	  wand03 			=> 1
	  wand04 			=> 1
	  wand05 			=> 1
	  wand06 			=> 1
	  wand07 			=> 1
	  wand11 			=> 1
	  wand99 			=> 1
    END
  END
END
//__________________________________________________________________________________

