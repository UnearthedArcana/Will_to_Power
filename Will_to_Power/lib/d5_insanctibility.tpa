// v2 - only one cancel spell

//INVISIBILITY + SANCTUARY MACRO______________________________________________________
//
DEFINE_ACTION_MACRO D5_INSANCTIBILITY BEGIN

  COPY_EXISTING ~sw1h01.itm~ ~override/d5__insanctibility.d5~

  APPEND ~splprot.2da~ ~D5_INSANCT%TAB%63%TAB%1%TAB%4~ UNLESS ~D5_INSANCT~

  COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_INSANCT~ BEGIN
	    SET d5_sanct_row = %row%
	  END
	END
  BUT_ONLY

//change the sanctuary visual effect__________________________________________________
  COPY_EXISTING ~sppr109.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 153 parameter2 = 1 STR_VAR resource = ~~ END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 153 opcode = 20 parameter1 = 0 parameter2 = 0 END 
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 153 opcode = 59 parameter1 = 25 parameter2 = 0 STR_VAR insert = ~first~ END 
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 153 opcode = 275 parameter1 = 25 parameter2 = 0 STR_VAR insert = ~first~ END 
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 153 opcode = 66 parameter1 = 128 parameter2 = 0 STR_VAR insert = ~first~ END 
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 153 opcode = 177 target = 1 parameter2 = 2 parameter1 = 0 STR_VAR resource = ~d5pr109~ insert = ~last~ END 

  CREATE EFF ~d5pr109x~
	WRITE_LONG 0x10 146
	WRITE_LONG 0x14 2
	WRITE_LONG 0x20 1
	WRITE_LONG 0x24 4
	WRITE_LONG 0x28 2
	WRITE_SHORT 0x2c 100
	WRITE_ASCII 0x30 ~D5PR109X~ #8

  COPY_EXISTING ~spwi206.spl~ ~override/d5pr109x.spl~
	SAY NAME1 ~~
	SAY UNIDENTIFIED_DESC ~~
	WRITE_SHORT 0x1c 4
	WRITE_LONG 0x1e 0
	WRITE_BYTE 0x25 0
	WRITE_BYTE 0x27 0
	WRITE_LONG 0x34 1
	LPF ALTER_SPELL_HEADER INT_VAR target = 1 range = 999 END
	LPF DELETE_EFFECT END


//patch all normal invisibility spells________________________________________________
  OUTER_SET invis_spell_index = 0

  OUTER_SPRINT $d5_invisibility_spells(~%invis_spell_index%~ ~timing~ ~duration~) ~spell~

  COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
   PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	SET invis_spell = 0
	READ_LONG 0x64 abil_offset
	READ_SHORT 0x68 abil_number
	READ_LONG 0x6a eff_offset
	PATCH_IF (abil_number > 0) BEGIN
	  READ_SHORT (abil_offset + 0x1e) eff_number
	  WHILE (%eff_number% > 0) BEGIN
		SET eff_number = (%eff_number% - 1)
	  	READ_SHORT (%eff_offset% + (eff_number * 0x30)) eff_opcode
	  	READ_BYTE (%eff_offset% + (eff_number * 0x30) + 0x2) eff_target
	  	READ_SHORT (%eff_offset% + (eff_number * 0x30) + 0x8) param_2
	  	READ_BYTE (%eff_offset% + (eff_number * 0x30) + 0xc) eff_timing
	  	READ_LONG (%eff_offset% + (eff_number * 0x30) + 0xe) eff_duration
	  	PATCH_IF (%eff_opcode% = 20) BEGIN
		  PATCH_IF (%param_2% = 0) BEGIN
			SET invis_spell = 1
			SET invis_target = %eff_target%
			SET invis_timing = %eff_timing%
			SET invis_duration = %eff_duration%
			SET ++invis_spell_index
		  END
		  PATCH_IF (%param_2% = 1) BEGIN
			SET invis_spell = 0
			SET eff_number = 0
		  END
		END
	  END
	  PATCH_IF (invis_spell = 1) BEGIN
		TEXT_SPRINT $d5_invisibility_spells(~%invis_spell_index%~ ~%invis_target%~ ~%invis_timing%~ ~%invis_duration%~) ~%SOURCE_RES%~
	  END
	END
   END
  BUT_ONLY

  ACTION_PHP_EACH d5_invisibility_spells AS index => spell BEGIN
	ACTION_IF (%index% > 0) BEGIN
	  PRINT ~%index% - %spell% - %index_1% - %index_2%~
	  COPY_EXISTING ~%spell%.spl~ ~override~
		LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 20 match_parameter2 = 0 opcode = 153 parameter2 = 1 STR_VAR resource = ~~ END
		LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 20 match_parameter2 = 0 opcode = 66 parameter1 = 128 parameter2 = 0 STR_VAR insert = ~first~ END
		LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 20 opcode = 272 parameter1 = 3 parameter2 = 3 STR_VAR resource = ~d5pr109x~ insert = ~last~ END 
	  COPY_EXISTING ~d5pr109x.spl~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 power = 0 parameter1 = 0 timing = 1 STR_VAR resource = EVAL ~%spell%~ END
	END
  END

//patch all normal invisibility items_________________________________________________
  OUTER_SET invis_item_index = 0

  OUTER_SPRINT $d5_invisibility_items(~%invis_item_index%~ ~timing~ ~duration~) ~item~

  COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
   PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	SET invis_item = 0
	READ_LONG 0x64 abil_offset
	READ_SHORT 0x68 abil_number
	READ_LONG 0x6a eff_offset
	PATCH_IF (abil_number > 0) BEGIN
	  READ_SHORT (abil_offset + 0x1e) eff_number
	  WHILE (%eff_number% > 0) BEGIN
		SET eff_number = (%eff_number% - 1)
	  	READ_SHORT (%eff_offset% + (eff_number * 0x30)) eff_opcode
	  	READ_BYTE (%eff_offset% + (eff_number * 0x30) + 0x2) eff_target
	  	READ_SHORT (%eff_offset% + (eff_number * 0x30) + 0x8) param_2
	  	READ_BYTE (%eff_offset% + (eff_number * 0x30) + 0xc) eff_timing
	  	READ_LONG (%eff_offset% + (eff_number * 0x30) + 0xe) eff_duration
	  	PATCH_IF (%eff_opcode% = 20) BEGIN
		  PATCH_IF (%param_2% = 0) BEGIN
			SET invis_item = 1
			SET invis_timing = %eff_target%
			SET invis_timing = %eff_timing%
			SET invis_duration = %eff_duration%
			SET ++invis_item_index
		  END
		  PATCH_IF (%param_2% = 1) BEGIN
			SET invis_item = 0
			SET eff_number = 0
		  END
		END
	  END
	  PATCH_IF (invis_item = 1) BEGIN
		TEXT_SPRINT $d5_invisibility_items(~%invis_item_index%~ ~%invis_target%~ ~%invis_timing%~ ~%invis_duration%~) ~%SOURCE_RES%~
	  END
	END
   END
  BUT_ONLY
  
  ACTION_PHP_EACH d5_invisibility_items AS index => item BEGIN
	ACTION_IF (%index% > 0) BEGIN
	  PRINT ~%index% - %item% - %index_1% - %index_2%~
	  COPY_EXISTING ~%item%.itm~ ~override~
		LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 20 match_parameter2 = 0 opcode = 153 parameter2 = 1 STR_VAR resource = ~~ END
		LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 20 match_parameter2 = 0 opcode = 66 parameter1 = 128 parameter2 = 0 STR_VAR insert = ~first~ END
		LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 20 opcode = 272 parameter1 = 3 parameter2 = 3 STR_VAR resource = ~d5pr109x~ insert = ~last~ END 
	  COPY_EXISTING ~d5pr109x.spl~ ~override~
		LPF ALTER_SPELL_HEADER INT_VAR target = 1 range = 999 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 power = 0 parameter1 = 0 timing = 1 STR_VAR resource = EVAL ~%item%~ END
	END
  END

  COPY_EXISTING ~d5pr109x.spl~ ~override~
	LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 2 power = 0 parameter2 = %d5_sanct_row% timing = 1 STR_VAR resource = ~d5pr109x~ END

END
//____________________________________________________________________________________
